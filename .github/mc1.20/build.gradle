plugins {
    id 'java'
    id 'io.github.goooler.shadow' version '8.1.5'
}

allprojects {
    apply plugin: 'java'

    group = 'nl.knokko.customitems'

    repositories {
        maven {
            url = 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/'
            content {
                includeGroup 'org.bukkit'
                includeGroup 'org.spigotmc'
            }
        }
        maven { url = 'https://oss.sonatype.org/content/repositories/snapshots' }
        maven { url = 'https://oss.sonatype.org/content/repositories/central' }
        mavenLocal()
        mavenCentral()
        maven { url = 'https://repo.papermc.io/repository/maven-public/' }
        maven { url = 'https://jitpack.io' }
    }
}

project(':kci-nms') {
    dependencies {
        compileOnly 'org.bukkit:bukkit:1.12.2-R0.1-SNAPSHOT'
    }
}

project(':kci-nms13plus') {
    dependencies {
        compileOnly project(':kci-nms')
        compileOnly 'org.bukkit:bukkit:1.13.2-R0.1-SNAPSHOT'
    }
}

project(':kci-nms16plus') {
    dependencies {
        compileOnly project(':kci-nms')
        compileOnly project(':kci-nms13plus')
        compileOnly 'org.spigotmc:spigot-api:1.16.5-R0.1-SNAPSHOT'
    }
}

project(':kci-nms18plus') {
    dependencies {
        compileOnly project(':kci-nms')
        compileOnly project(':kci-nms13plus')
        compileOnly project(':kci-nms16plus')
        compileOnly 'org.spigotmc:spigot-api:1.18.2-R0.1-SNAPSHOT'
    }
}

project(':kci-nms20') {
    apply plugin: 'io.github.goooler.shadow'
    dependencies {
        compileOnly project(':kci-nms')
        compileOnly project(':kci-nms13plus')
        compileOnly project(':kci-nms16plus')
        compileOnly project(':kci-nms18plus')
        compileOnly 'io.papermc.paper:paper-api:1.20.6-R0.1-SNAPSHOT'
        implementation("com.github.knokko:DataComponentAPI:b8f529f78e86b357b5d24ccad95408c9772ef4aa")
    }
    shadowJar {
        relocate('kr.toxicity.libraries.datacomponent', 'nl.knokko.customitems.plugin.shadow.datacomponent')
    }
}
